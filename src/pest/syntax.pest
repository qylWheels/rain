WHITESPACE = _{ " " | "\t" }

// Trival lexical elements.
int = @{ "-"? ~ ASCII_DIGIT+ }
id = @{ ASCII_ALPHA+ }

// Keywords.
if = { "if" }
then = _{ "then" }
else = _{ "else" }
true = { "true" }
false = { "false" }
let = { "let" }
in = _{ "in" }

// Operators.
binop = _{ plus | times | le | eq }
plus = { "+" }
times = { "*" }
le = { "<=" }
eq = { "=" }

// Parentheses.
lparen = _{ "(" }
rparen = _{ ")" }

// Syntax.
prog = _{ SOI ~ expr ~ EOI }
expr = _{
    (
        int
        | true
        | false
        | let ~ id ~ eq ~ expr ~ in ~ expr
        | if ~ expr ~ then ~ expr ~ else ~ expr
        | id
        | lparen ~ expr ~ rparen
    ) ~ expr2
}
expr2 = _{ binop ~ expr ~ expr2 | "" }
